100 REM PROG2.BAS imprint data set program by canon software corporation
110          SCREEN 0
120 COLOR   7,0,0
130 CLS:KEY OFF:KEY 2,"":FOR I=6 TO 9 : KEY I,"": NEXT
140 ON ERROR GOTO 2520
150 DEFSTR W-Y:DEFINT A-V: OPTION BASE 1: DEF USR0=2
155 DIM SETD(36,9),SD(36),NOTE$(36),ARAY1(40),ARAY2(40),ARAY3(40),IMCUP(8,2),UNBAR$(3),W9(10),PCCHR(8),TBCHR(8),NUMF$(4)
160 DEF FNBCD(AD)=(PEEK(AD)\&H10)*10+(PEEK(AD) MOD &H10)
170 SUBRT1=0:SUBRT2=0:SUBRT3=0:IFU=0:STORE=0: DATADD1=&H300 : DATADD2=&H800
180 D10=4:ST=0:B1=8 :B2=2:B3=72:B4=6
190 F2=4: D11=1:INIT=0:RW=0:F3=1:F4=1:F5=1:V1=0:V2=0:V3=0:IMCU=1:BSPACE=8:SEF=1
200  L1=B1-(F2-1)*D11:L2=B4+(F2-1)*D10:CX=L1+1: CY=B2+(F2-1)*D10 :RW2=0:RW3=0
210 FOR I=1 TO 36:FOR J=1 TO 9:SETD(I,J)=0:NEXT J:SD(I)=0:NEXT I
220 UNBAR$(1)=STRING$(30,"-"):UNBAR$(2)=STRING$(20,"-")+SPACE$(10):UNBAR$(3)=STRING$(10,"-")+SPACE$(20)
230 F5$="NOTE"
250 F1=1:W1="DATE":W2="TIME":W3="NO.":W4="TVAV":W5="MODE":W6="f"
260 W7="[ MODE SET, PRESS SPACEBAR ]":W8="[ END MODE SET--->ENTER ]":W10="[ DATA SET, PRESS SPACEBAR ]
270 W11=CHR$(218)+CHR$(196)+" COPY "+CHR$(196)+CHR$(191): W12="PRESET AGAIN ---> [ ENTER ]"
280 B$=SPACE$(30)
281 FOR I=1 TO 8:READ D:PCCHR(I)=D:NEXT I
282 FOR I=1 TO 8:READ D:TBCHR(I)=D:NEXT I
283 DATA 128,165,156,142,225,153,154,157
284 DATA 128,144,156,224,225,228,230,92
290 FOR I=1 TO 36:NOTE$(I)=B$:NEXT I
295 FOR I=1 TO 4: READ D$ :NUMF$(I)=D$ :NEXT I :'NUMBER OF DATA
296 DATA 12EX,20EX,24EX,36EX
298 W20="FILM SET": W21="PRESS CURSOR KEY": W22="[ END SET ---> ENTER ]" : NUMF=1 : W23="WARNING:" : W24="Select one from within the number of exposures": W25="in use from below."
320 REM  ****  set sub program & area clr ***
330 DEF SEG=0
340 ZSEG=PEEK(&H510)+(256*PEEK(&H511))+&H1001 : REM  segment  ID
350 SEGID=VAL("&H"+HEX$(ZSEG))
360 DEF SEG=SEGID
370 FOR I1=0 TO 35
380    FOR I2=0 TO 19
390       POKE DATADD1+I1*32+I2,&H20
400    NEXT I2
410    POKE DATADD1+I1*32+30,&HFF: POKE DATADD1+I1*32+31,0
420 NEXT I1
430 BLOAD"BSUB1.COM",0: DEF SEG
440 SUBRT1=VARPTR(ARAY1(1)):BLOAD "SUBRT1.COM",SUBRT1
450 SUBRT2=VARPTR(ARAY2(1)):BLOAD "SUBRT2.COM",SUBRT2
460 SUBRT3=VARPTR(ARAY3(1)):BLOAD "SUBRT3.COM",SUBRT3
470 GOTO 1100 :REM  to main
480 REM
490 ARAY1(3)=P3:ARAY1(4)=P4:ARAY1(5)=QY:ARAY1(6)=D3:ARAY1(7)=C3
500 SUBRT1=VARPTR(ARAY1(1)):CALL SUBRT1
510 RETURN
520 ARAY2(3)=Q3:ARAY2(4)=Q4:ARAY2(5)=PX:ARAY2(6)=D4:ARAY2(7)=C4
530 SUBRT2=VARPTR(ARAY2(1)):CALL SUBRT2
540 RETURN
545 REM  **  write  box  **
550 P3=P1:P4=P2:QY=Q1-1:C3=C1:D3=196:GOSUB 490
560 QY=Q2:D3=196:GOSUB 490
570 P3=P1:P4=P1:QY=Q1-1:C3=C1:D3=218:GOSUB 490
580 P3=P2:P4=P2:QY=Q1-1:C3=C1:D3=191:GOSUB 490
590 PX=P1:Q3=Q1:Q4=Q2-1:C4=C1:D4=179:GOSUB 520
600 PX=P2:GOSUB 520
610 P3=P1:P4=P1:QY=Q2  :C3=C1:D3=192:GOSUB 490
620 P3=P2:P4=P2:QY=Q2  :C3=C1:D3=217:GOSUB 490
630 RETURN
640  P1=B1:Q1=B2:P2=B3:Q2=B4:C1=3
650  FOR I=1 TO F2
660  IF (INIT=0) THEN GOSUB 550
670  LOCATE B2+D10*(I-1)  ,B1-(I-1)+1:PRINT " ";
680  LOCATE B2+D10*(I-1)  ,B1-(I-1)+2:PRINT USING "[##]";F1+F2-I;
690  LOCATE B2+D10*(I-1)+1,B1-(I-1)+3:PRINT   F5$+":";B$+" ";
700  LOCATE B2+D10*(I-1)+2,B1-(I-1)+7:PRINT  B$+"  ";
710 IF (INIT<>0) THEN CUR=F1+F2-I:GOSUB 820
720 IF (INIT<>0) THEN 740
730 IF(I<>F2) THEN P3=P1:P4=P2-1:QY=Q2:D3=&H20:C3=0:GOSUB 490
740 IF (I<> 1) THEN  P3=P1+D11:P4=P1:QY=Q1-1:D3=193:C3=C1:GOSUB 490
750 P1=P1-D11:P2=P2-D11:Q1=Q1+D10:Q2=Q2+D10
760 NEXT I
770 RETURN
780 P1=L1:V1=P1:Q1=21:V2=Q1:P2=B3:Q2=25:V3=Q2-1:C1=4:GOSUB 550:RETURN
790 GOSUB 1220: Q1=23 :Q2=24
800 P3=CX:P4=P3:QY=CY :D3=219:C3=12:GOSUB 490
810 RETURN
820 RW=CX+9-D11*(F3-CUR)
830 RW3=CX+8-D11*(F3-CUR):RW2=CY+D10*(F3-CUR)
850 LOCATE RW2,RW:PRINT B$
860 FOR J=1 TO 7
870 IF (J=7) THEN 950 ELSE IF (SETD(CUR,J)=0) THEN 990
880 IF (J=1) THEN LOCATE RW2,RW:PRINT W1:RW=RW+5
890 IF (J=2) THEN LOCATE RW2,RW:PRINT W2:RW=RW+5
900 IF (J=3) THEN LOCATE RW2,RW:PRINT W3:RW=RW+4
910 IF (J=4) THEN LOCATE RW2,RW:PRINT W4:RW=RW+5
920 IF (J=5) THEN LOCATE RW2,RW:PRINT W5:RW=RW+5
930 IF (J=6) THEN LOCATE RW2,RW:PRINT W6:RW=RW+3
940 GOTO 990
950 IF (SETD(CUR,7)<>0) THEN LOCATE RW2,RW :PRINT F5$: RW=RW+5
960 IF (SETD(CUR,8)=0) THEN LOCATE RW2+1,RW3 :PRINT SPACE$(30):LOCATE RW2+2,RW3 :Q=30-10*SETD(CUR,9) : GOTO 980
970 LOCATE RW2+1,RW3 :PRINT MID$(NOTE$(CUR),1,SETD(CUR,8));SPACE$(30-SETD(CUR,8)):LOCATE RW2+2,RW3 :Q=30-10*SETD(CUR,9)
980 IF (SETD(CUR,7)<>0) THEN PRINT UNBAR$(SETD(CUR,9)); ELSE PRINT B$;
990 NEXT J
1000 RETURN
1010 IMCU=1:LOCATE V2,V1+1:PRINT W7:LOCATE V2,V1+32:PRINT W8
1020 LOCATE V2+1,V1+4:PRINT W1:IMCUP(1,1)=V1+3-1:IMCUP(1,2)=V2+1:                    LOCATE V2+1,V1+10:PRINT W2:IMCUP(2,1)=V1+9-1:IMCUP(2,2)=V2+1:                    LOCATE V2+1,V1+16:PRINT W3:IMCUP(3,1)=V1+15-1:IMCUP(3,2)=V2+1
1030 LOCATE V2+1,V1+21:PRINT W4:IMCUP(4,1)=V1+20-1:IMCUP(4,2)=V2+1
1040 LOCATE V2+1,V1+27:PRINT W5:IMCUP(5,1)=V1+26-1:IMCUP(5,2)=V2+1:                  LOCATE V2+1,V1+33:PRINT W6:IMCUP(6,1)=V1+32-1:IMCUP(6,2)=V2+1
1050 LOCATE V2+2,V1+4:PRINT F5$+":":IMCUP(7,1)=V1+3-1:IMCUP(7,2)=V2+2:IMCUP(8,1)=    IMCUP(7,1)+7:IMCUP(8,2)=IMCUP(7,2)
1060 QY=IMCUP(IMCU,2):P3=IMCUP(IMCU,1):P4=P3 :D3=219:C3=12:GOSUB 490
1070 RETURN
1080 ARAY3(3)=P5:ARAY3(4)=P6:ARAY3(5)=QY1:ARAY3(6)=C5:SUBRT3=VARPTR(ARAY3(1)):CALL SUBRT3
1090 RETURN
1095 REM *** main ***
1100 GOSUB 1270: '  set number of film
1105 GOSUB 790: GOSUB 2800
1110 ON KEY (1)  GOSUB 1240:REM input mode
1115 ON KEY (2)  GOSUB 1270:REM number of film
1120 ON KEY (3)  GOSUB 2270:REM ifu->t/b  ( send )
1130 ON KEY (4)  GOSUB 1380:REM mode copy
1140 ON KEY (5)  GOSUB 1450:REM note copy
1150 ON KEY (10) GOSUB 2510:REM end
1160 ON KEY (11) GOSUB 1520:REM up
1165 ON KEY (12) GOSUB 1590:REM <-
1170 ON KEY (13) GOSUB 1650:REM ->
1175 ON KEY (14) GOSUB 1890:REM down
1180 KEY (1) ON: KEY (2) ON: KEY (3) ON: KEY (4) ON: KEY (5) ON: KEY (10) ON
1190 KEY (11) ON : KEY (14) ON: MDI=0: SDI=1
1200 IF (INKEY$<>"") THEN GOTO 1200
1210 IF MDI=1 THEN 1180
1212 IF MDI=2 THEN 1105 ELSE 1200
1220 FOR I=L2+1 TO 25 :P3=0  :P4=79 :QY=I:D3=&H20:C3=0:GOSUB 490:NEXT I: RETURN
1235 REM *** input mode ***
1240 IF MDI=0 THEN MDI=2 ELSE RETURN
1242 GOSUB 2190:GOSUB 1220:KEY OFF :GOSUB 780:GOSUB 1010:ST=1:F4=F3:F5=F3:GOSUB 2210
1244 FOR I=1 TO 7
1246 IF (SETD(F3,I)<>0) THEN C5=&H84:P5=IMCUP(I,1):QY1=IMCUP(I,2) ELSE 1252
1248 IF (I=3) THEN P6=P5+4 ELSE IF (I=7) THEN P6=P5+6 ELSE P6=P5+5
1250 GOSUB 1080
1252 NEXT I
1253 GOSUB 1060
1254 IF (INKEY$<>"") THEN 1254
1256 KEY(12) ON: KEY(13) ON: SDI=0
1258 IF (SDI <> 0) THEN 1254
1260 Y1=INKEY$:IF (Y1="") THEN 1258
1262 SDI=1:KEY (12) STOP:KEY (13) STOP
1264 IF (Y1<>" ") THEN ELSE GOSUB 1960:GOTO 1254
1266 IF (ASC(Y1)<>13) THEN 1254 ELSE GOSUB 2060: SDI=1: RETURN
1268 REM *** number of film ***
1270 IF MDI=0 THEN MDI=2 ELSE RETURN
1272 GOSUB 2190 :'  FUNCTION KEY OFF
1274 CLS        :P1=3:Q1= 2:P2=78:Q2=25:C1= 2:GOSUB 550
1275 P1=5:Q1=10:P2=73:Q2=14:C1= 4:GOSUB 550
1276 LOCATE  5,7 :PRINT W20;
1277 LOCATE 11,7 :PRINT W23; "  ";W24:LOCATE 12,17:PRINT W25;
1278 LOCATE 20,25 :PRINT W21;
1279 LOCATE 24,25 :PRINT W22;
1280 FOR I=1 TO 4
1282    LOCATE 22,15+14*(I-1):PRINT NUMF$(I);
1284 NEXT I
1286 ON KEY (12) GOSUB 1320 :' <- film set
1287 ON KEY (13) GOSUB 1340 :' -> film set
1288 IF (INKEY$<>"") THEN 1288
1289 C5=&H84: P5=15+14*(NUMF-1): P6=P5+4: QY1=22: GOSUB 1080
1290 KEY (12) ON : KEY (13) ON : SDI=0
1291 IF (SDI <> 0) THEN 1288
1292 Y1=INKEY$ :IF Y1="" THEN 1291
1293 KEY (12) STOP : KEY (13) STOP
1294 IF ASC(Y1) <> 13 THEN 1288
1295 CLS        : F1=1:F3=1:F4=1:F5=1:CUR=1
1296 L1=B1-(F2-1)*D11:L2=B4+(F2-1)*D10:CX=L1+1: CY=B2+(F2-1)*D10
1297 INIT=0: GOSUB 640 : INIT =1: GOSUB 640
1298 IF NUMF = 1 THEN F3MAX=12 :GOTO 1310
1300 IF NUMF = 2 THEN F3MAX=20 :GOTO 1310
1302 IF NUMF = 3 THEN F3MAX=24 ELSE F3MAX=36
1310 RETURN
1319 '    <- film set
1320 IF SDI=0 THEN SDI=3 ELSE RETURN
1321 KEY (12) STOP : KEY (13) STOP
1322 C5=7   : P5=15+14*(NUMF-1): P6=P5+4: QY1=22: GOSUB 1080 : '
1324 IF NUMF >1 THEN NUMF=NUMF-1
1326 RETURN
1339 '    -> film set
1340 IF SDI=0 THEN SDI=3 ELSE RETURN
1341 KEY (12) STOP : KEY (13) STOP
1342 C5=7   : P5=15+14*(NUMF-1): P6=P5+4: QY1=22: GOSUB 1080 : '
1344 IF NUMF =<3 THEN NUMF=NUMF+1
1346 RETURN
1379 REM *** mode  copy ***
1380 IF MDI=0 THEN MDI=1 ELSE RETURN
1390 IF (ST<>0) THEN  RETURN ELSE GOSUB 2190
1400 IF (F3=1) THEN BEEP: RETURN
1410 F4=F4-1: IF (F4<=0) THEN F4=F3-1
1420 FOR J=1 TO 7 :SETD(F3,J)=SETD(F4,J):NEXT J:SETD(F3,9)=SETD(F4,9)
1430 CUR=F3: GOSUB 820
1440 GOSUB 2130: RETURN
1445 REM *** note  copy ***
1450 IF MDI=0 THEN MDI=1 ELSE RETURN
1460 IF (ST<>0) THEN  RETURN ELSE GOSUB 2190
1470 IF (F3=1) THEN BEEP: RETURN
1480 F5=F5-1: IF (F5<=0) THEN F5=F3-1
1490 SETD(F3,8)=SETD(F5,8):NOTE$(F3)=NOTE$(F5)
1500 CUR=F3: GOSUB 820
1510 GOSUB 2080: RETURN
1515 REM ** up **
1520 IF MDI=0 THEN MDI=1 ELSE RETURN
1530 IF (ST<>0) THEN RETURN ELSE GOSUB 2190:INIT=1:P3=CX:P4=CX:QY=CY:D3=&H20:C3=0:GOSUB 490:CX=CX+D11:CY=CY-D10
1540 F3=F3+1:F4=F3:F5=F3: IF(F3>F3MAX) THEN F3=F3MAX:F4=F3:F5=F3
1550 IF (CX>(B1+1)) THEN CX=B1+1: CY=B2  :F1=F1+1 : ELSE 1580
1560 IF (F1>(F3MAX-F2+1))  THEN  F1=F3MAX-F2+1:BEEP:GOTO 1580
1570 GOSUB 640 :P3=CX:P4=CX:QY=CY:D3=219:C3=12:GOSUB 490 :RETURN
1580            P3=CX:P4=CX:QY=CY:D3=219:C3=12:GOSUB 490 :RETURN
1585 REM ** <- **
1590 IF SDI=0 THEN SDI=1 ELSE RETURN
1600 IF ST=0 THEN RETURN ELSE KEY (12) STOP:KEY (13) STOP
1610 IF (IMCU=8) THEN RETURN ELSE QU=IMCUP(IMCU,2):P3=IMCUP(IMCU,1):P4=P3:D3=&H20:C3=0:GOSUB 490
1620 IF (IMCU=1) THEN IMCU=7 ELSE IMCU=IMCU-1
1630 QY=IMCUP(IMCU,2):P3=IMCUP(IMCU,1):P4=P3 :D3=219:C3=12:GOSUB 490
1640 RETURN
1645 REM ** -> **
1650 IF SDI=0 THEN SDI=1 ELSE RETURN
1660 IF ST=0 THEN RETURN ELSE KEY (12) STOP:KEY (13) STOP
1670 IF (IMCU=8) THEN 1730 ELSE QY=IMCUP(IMCU,2):P3=IMCUP(IMCU,1):P4=P3:D3=&H20:C3=0:GOSUB 490
1680 IMCU=IMCU+1
1690 IF(IMCU=8)THEN 1720
1700 QY=IMCUP(IMCU,2):P3=IMCUP(IMCU,1):P4=P3 :D3=219:C3=12:GOSUB 490
1710 RETURN
1715 REM ** note **
1720 IF( SETD(F3,8)=0 ) THEN QY=IMCUP(IMCU,2):P3=IMCUP(IMCU,1):P4=P3 :D3=219:C3=12:GOSUB 490
1730 KEY(11) OFF:KEY(12) OFF:KEY(13) OFF:KEY(14) OFF
1740 IF (INKEY$<>"") THEN 1740
1750 QY=IMCUP(IMCU,2):P3=IMCUP(IMCU,1):P4=P3 :D3=219:C3=12:GOSUB 490
1770 Y1=INKEY$:IF (Y1="") THEN 1770
1780 IF (ASC(Y1)=13) THEN 1850
1790 IF (ASC(Y1)<>BSPACE ) THEN 1810
1800 IF (SETD(F3,8)=0) THEN 1740 ELSE MID$(NOTE$(F3),SETD(F3,8))=" ":SETD(F3,8)=SETD(F3,8)-1:QY=IMCUP(IMCU,2):P3=IMCUP(IMCU,1):P4=P3:D3=&H20:C3=0:GOSUB 490:IMCUP(IMCU,1)=IMCUP(IMCU,1)-1:GOTO 1740
1810 Y1$=MID$(Y1$,1,1)
1811 FOR J=1 TO 8
1812     IF ASC(Y1)=PCCHR(J) THEN 1830
1813 NEXT J
1815 IF ((Y1>="a") AND (Y1=<"z")) THEN Y1=CHR$(ASC(Y1)-ASC("a")+ASC("A"))
1820 IF ((Y1>=" ") AND (Y1<"a")) OR ((Y1>="{") AND (Y1=<"~"))THEN ELSE 1740
1830 IF (SETD(F3,8)=30) THEN BEEP:GOTO 1740
1840 QY=IMCUP(IMCU,2):P3=IMCUP(IMCU,1):P4=P3:D3=ASC(Y1) :C3=7 :GOSUB 490:IMCUP(IMCU,1)=IMCUP(IMCU,1)+1:SETD(F3,8)=SETD(F3,8)+1: MID$(NOTE$(F3),SETD(F3,8))=Y1:GOTO 1740
1850 KEY(11) STOP:KEY(12) STOP:KEY(13) STOP:KEY(14) STOP
1860 QY=IMCUP(IMCU,2):P3=IMCUP(IMCU,1):P4=P3:D3=&H20:C3=0:GOSUB 490
1870 IMCU=7:QY=IMCUP(IMCU,2):P3=IMCUP(IMCU,1):P4=P3:D3=219 :C3=12:GOSUB 490
1880 Y1="" : RETURN
1885 REM ** down **
1890 IF MDI=0 THEN MDI=1 ELSE RETURN
1900 IF(ST<>0) THEN RETURN ELSE GOSUB 2190:INIT=1:P3=CX:P4=CX:QY=CY:D3=&H20:C3=0:GOSUB 490:CX=CX-D11:CY=CY+D10
1910 F3=F3-1:F4=F3:F5=F3:IF(F3<1 ) THEN F3=1 :F4=F3:F5=F3
1920 IF (CX<(B1-(F2-1)+1)) THEN CX=B1-(F2-1)+1: CY=B2+(F2-1)*D10 :F1=F1-1 : ELSE 1950
1930 IF (F1<1 ) THEN  F1=1:BEEP :GOTO 1950
1940 GOSUB 640 :P3=CX:P4=CX:QY=CY:D3=219:C3=12:GOSUB 490 :RETURN
1950            P3=CX:P4=CX:QY=CY:D3=219:C3=12:GOSUB 490 :RETURN
1958 '   * * hit space bar * *
1960 IF (ST=O) THEN RETURN
1970 IF (IMCU>=8) THEN RETURN
1980 IF (SETD(F3,9)<=2) THEN 2000
1990 IF (SETD(F3,IMCU)=0) THEN BEEP:RETURN
2000 SETD(F3,IMCU)=SETD(F3,IMCU) XOR 1
2010 IF SETD(F3,IMCU)=0 THEN C5=7:SETD(F3,9)=SETD(F3,9)-1: ELSE SETD(F3,9)=SETD(F3,9)+1:C5=&H84
2020 P5=IMCUP(IMCU,1)+2:IF (IMCU=3) THEN P6=P5+3 ELSE IF (IMCU=8) THEN P6=P5+5 ELSE P6=P5+4
2030 QY1=IMCUP(IMCU,2):GOSUB 1080
2040 GOSUB 2240
2050 RETURN
2060 ST=0:IMCU=1:GOSUB 1220:CUR=F3:GOSUB 820
2070 REM *** write segment (NOTE) ***
2080 K=32*(F3-1)
2090 DEF SEG=SEGID
2100 STORE=DATADD1
2101 FOR I=1 TO 30
2102     D=ASC(MID$(NOTE$(F3),I))
2103     FOR J=1 TO 8
2104         IF D=PCCHR(J) THEN D=TBCHR(J):GOTO 2108
2105     NEXT J
2108     POKE (STORE+I+K-1),D
2109 NEXT I: POKE (STORE+K+30),&HFF
2110 DEF SEG
2120 REM *** write segment (IMP.MODE) ***
2130 SD(F3)=0:K=1:FOR J=1 TO 7: IF(SETD(F3,J)<>0) THEN SD(F3)=SD(F3) OR K
2140 K=2*K:NEXT J
2150 IF (NOTE$(F3) <> B$) THEN SD(F3)=SD(F3) OR &H80
2160 K=32*(F3-1)
2170 DEF SEG=SEGID: POKE (STORE+K+31),SD(F3): DEF SEG
2180 RETURN
2189 ' ** KEY STOP **
2190 KEY (1) STOP: KEY (3) STOP: KEY (4) STOP: KEY (5) STOP:KEY (10) STOP
2200 KEY (11) STOP: KEY (12) STOP: KEY (13) STOP: KEY (14) STOP:RETURN
2210 REM  print note & ---
2220 IMCUP(8,1)=IMCUP(7,1)+7:LOCATE IMCUP(8,2),IMCUP(8,1):PRINT B$
2230 IF (SETD(F3,8)<>0) THEN LOCATE IMCUP(8,2),IMCUP(8,1):PRINT NOTE$(F3):IMCUP(8,1)=IMCUP(8,1)+SETD(F3,8)
2240 LOCATE IMCUP(8,2)+1,IMCUP(7,1)+7
2250 IF (SETD(F3,7)<>0) THEN PRINT UNBAR$(SETD(F3,9)); ELSE PRINT B$;
2260 RETURN
2265 REM *** ifu -> t/b ***  ( send )
2270 IF MDI=0 THEN MDI=2 ELSE RETURN
2280 IF (ST<>0) THEN RETURN ELSE KEY OFF:GOSUB 2190:GOSUB 1220
2290 GOSUB 780:F4=F3:F5=F3
2300 LOCATE V2+1,V1+4:PRINT W10;SPACE$(5);W12;
2301 DEF SEG=SEGID
2302 FOR I=1 TO F3MAX :IF I=36 THEN 2305
2303 IF SD(I+1) <> 0 THEN POKE DATADD1+32*I+31,SD(I+1): GOTO 2305
2304 IF SD(I)=0 THEN POKE DATADD1+32*I+31,&HFF ELSE POKE DATADD1+32*I+31,0
2305 NEXT I
2306 IF NUMF=4 THEN 2309
2307 FOR I=F3MAX+1 TO 35 : POKE DATADD1+32*I+31,&HFF : NEXT I
2308 POKE DATADD1+32*F3MAX+31,0
2309 DEF SEG
2310 IF (INKEY$<>"") THEN 2310
2320 Y2=INKEY$: IF Y2= "" THEN 2320 ELSE IF (Y2=" ") THEN 2340
2330 IF ( ASC(Y2)<>13 ) THEN 2320 ELSE GOSUB 1220: ST=0: RETURN
2340 LOCATE V2+1,V1+4:PRINT SPACE$(60)
2350 LOCATE V2+3,V1+4:PRINT SPACE$(60);
2360 SMSG$=CHR$(&H5A)+CHR$(&HA3)+CHR$(&HFA)+CHR$(&H1F)+CHR$(&HFC)+CHR$(&H1F)+CHR$(DATADD2 MOD &H100)+CHR$(DATADD2\&H100)+CHR$(3)+CHR$(0)+CHR$(1)
2370 GOSUB 2500: IF RVAL<>0 THEN 2490
2380 DEF SEG=SEGID: SEF=FNBCD(DATADD2): DEF SEG
2390 LOCATE V2+3,V1+4:IF SEF<F3MAX THEN 2410
2400 PRINT "FILM END";: GOTO 2310
2410 PRINT USING "[##] ---> [##] SET";SEF+1;F3MAX;
2420 DATADD3=DATADD1+32*SEF: DATADD4=&H180+32*SEF: DATLEN=&H5FF-DATADD4+1
2430 DEF SEG=SEGID: SECC=PEEK(DATADD3+31): DEF SEG
2431 IF SECC=&HFF THEN SECC=0
2435 SMSG$=CHR$(&H5A)+CHR$(&HA5)+CHR$(&H1)+CHR$(&H0)+CHR$(0)+CHR$(&H0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)
2436 GOSUB 2500: IF RVAL<>0 THEN 2490
2440 SMSG$=CHR$(&H5A)+CHR$(&HA7)+CHR$(&HE)+CHR$(&H80)+CHR$(SECC)+CHR$(&HFF)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)
2450 GOSUB 2500: IF RVAL<>0 THEN 2490
2452 SMSG$=CHR$(&H5A)+CHR$(&HAD)+CHR$(1)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)
2454 GOSUB 2500: IF RVAL<>0 THEN 2490
2460 SMSG$=CHR$(&H5A)+CHR$(&HA9)+CHR$(DATADD4 MOD &H100)+CHR$(DATADD4\&H100)+CHR$(&HFE)+CHR$(&H5)+CHR$(DATADD3 MOD &H100)+CHR$(DATADD3\&H100)+CHR$((DATLEN-1) MOD &H100)+CHR$((DATLEN-1)\&H100)+CHR$(2)
2470 GOSUB 2500: IF RVAL<>0 THEN 2490
2471 DEF SEG=SEGID: SLAST=PEEK(DATADD1+1151) : DEF SEG
2472 SMSG$=CHR$(&H5A)+CHR$(&HA5)+CHR$(&H1)+CHR$(&H0)+CHR$(0)+CHR$(&H0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)
2473 GOSUB 2500: IF RVAL<>0 THEN 2490
2474 SMSG$=CHR$(&H5A)+CHR$(&HA7)+CHR$(&HFF)+CHR$(&H85)+CHR$(SLAST)+CHR$(&HFF)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)
2475 GOSUB 2500: IF RVAL<>0 THEN 2490
2476 GOSUB 5000
2478 IF LAST<>SLAST THEN 2490:'SET CHECK
2480 LOCATE V2+3,V1+50:PRINT " SET OK !!";: GOTO 2300
2490 LOCATE V2+3,V1+50:PRINT "I/O ERROR!!";: GOTO 2300
2500 DEF SEG=SEGID: R$=USR0(SMSG$): RVAL=PEEK(0): DEF SEG: RETURN
2505 REM ***     end    ***
2510 IF MDI=0 THEN ELSE RETURN
2520 CLS: KEY 2,"RUN"+CHR$(13):KEY 6,","+CHR$(34)+"LPT1:"+CHR$(34)+CHR$(13):KEY 7,"TRON"+CHR$(13):KEY 8,"TROFF"+CHR$(13):KEY 9,"KEY":KEY ON
2530 ON ERROR GOTO 0 : END
2750 '*** DISPLAY KEY LIST ***
2800 RESTORE 3000: FY=2
2900 FOR I1=1 TO 10
2910   LOCATE 25,FY:PRINT I1 MOD 10;
2915   IF I1=2 THEN F$=NUMF$(NUMF) ELSE READ F$
2918   IF F$="" THEN FY=FY+5:GOTO 2960
2920   VADD=3840+2*(FY+1): DEF SEG=&HB800
2930   FOR I2=1 TO LEN(F$)
2940     POKE VADD+2*(I2-1),ASC(MID$(F$,I2,1)):POKE VADD+2*(I2-1)+1,&H70
2950   NEXT I2:DEF SEG:FY=FY+10
2960 NEXT I1
2965 LOCATE 24,35:PRINT W11;
2970  LOCATE 1,1:RETURN
3000 DATA IMP.MODE,IFU->TB,MODE,NOTE,,,,,END
5000 SMSG$=CHR$(&H5A)+CHR$(&HA3)+CHR$(&HFF)+CHR$(5)+CHR$(0)+CHR$(&H6)+CHR$(DATADD2 MOD &H100)+CHR$(DATADD2\&H100)+CHR$(2)+CHR$(0)+CHR$(1)
5010 GOSUB 2500: IF RVAL<>0 THEN 2490
5020 DEF SEG=SEGID: LAST=PEEK(DATADD2): SLAST=PEEK(DATADD1+32*36-1): DEF SEG
5030 RETURN

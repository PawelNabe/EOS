10 REM PROG3.BAS IFU-timer program by canon software corporation
20               SCREEN 0
30 COLOR   7,0,0
40 CLS:OPTION BASE 1:KEY OFF
50 DEFSTR W-Y:DEFINT A-V:DIM ARAY1(40),ARAY2(40),ARAY3(40),W9(10)
60 ON ERROR GOTO 4340
70 DEF SEG=0: ZSEG=PEEK(&H510)+(256*PEEK(&H511))+&H1001: DEF SEG
80 SEGID=VAL("&H"+HEX$(ZSEG)): DEF USR0=2: CBUF=&H300
90 DEF FNZEV(A)=SGN(A)*(INT(ABS(A)/4))+(A MOD 4)*.25
100 DEF FNBCD(AD)=10*(PEEK(AD)\16)+(PEEK(AD) MOD 16)
110 DIM DT(36,3),TM(36,2),AE(36,3),AB(36,5),VD(36,2),VT(36),SP(11,2),ER(36),CC(4)
120 DIM TV$(35),AV$(21),CT$(4)
130 SUBRT1=0:SUBRT2=0:IFU=0:STORE=0
140 D10=3:ST=0:B1=8 :B2=2:B3=72:B4=5:FSW=0
150 F2=4: D11=1: INIT=0: RW=0: F3=1: F4=1: F5=1
160  L1=B1-(F2-1)*D11:L2=B4+(F2-1)*D10:CXX=L1+1: CYY=B2+(F2-1)*D10:V1=L1:V2=L2+3:V3=23-1
170 F1$="SET":F3$="SETEND":F5$="START":F10$="END":FB2$="AEB":FB4$="CLEAR":FB5$="RESET"
180 W9(1)="LIST ":W9(2)="RUN"+CHR$(13):W9(3)="LOAD"+CHR$(34):W9(4)="SAVE"+CHR$(34):W9(5)="CONT"+CHR$(13):W9(6)=","+CHR$(34)+"LPT1"+CHR$(34)+CHR$(13):W9(7)="TRON"+CHR$(13):W9(8)="TROFF"+CHR$(13):W9(9)="KEY ":W9(10)="SCREEN 0,0,0"+CHR$(13)
190 F1=1:W1="DATE & TIME":W2="SHOOTING MODE":W3="step:":W4="from:":W5="fr  :":W6="to  :":W7="Group      :"
200 W8="[ END MODE SET--->ENTER ]"
210 B$=SPACE$(32)
220 FOR I=1 TO 35:READ D$:TV$(I)=D$:NEXT : REM  tv$ set
230 DATA 30",20",15",10",8",6",4",3",2",1"5,1",0"7,2,3,4,6,8,10,15,20,30,45,60,90,125,180,250,350,500,750,1000,1500,2000,3000,4000
240 FOR I=1 TO 21:READ D$:AV$(I)=D$:NEXT : REM  av$ set
250 DATA F1.0,F1.2,F1.4,F1.8,F2.0,F2.5,F2.8,F3.5,F4.0,F4.5,F5.6,F6.7,F8.0,F9.5,F11,F13,F16,F19,F22,F27,F32
260 REM cc set & ct$ set
270 FOR I=1 TO 4 :READ D:CC(I)=D:NEXT
280 DATA 4,6,5,7
290 FOR I=1 TO 4 :READ D$:CT$(I)=D$:NEXT
300 DATA P,TV,AV,M
310 REM
320 FOR I=1 TO 11 : FOR J=1 TO 2 : READ D:SP(I,J)=D :NEXT J:NEXT I
330 DATA 0,23,0,27,0,31,0,43,0,47,1,23,1,38,1,48,3,22,3,39,4,22
340 REM
350 FOR I=1 TO 36:FOR J=1 TO 3 :DT(I,J)=-1 :NEXT J :VD(I,1)=-1 :FOR J=1 TO 2:TM(I,J)=-1:NEXT J:AE(I,1)=1:AE(I,2)=25:AE(I,3)=11:AB(I,2)=-2:AB(I,4)=2:AB(I,5)=3:ER(I)=1:NEXT I
360 SUBRT1=VARPTR(ARAY1(1)):BLOAD "SUBRT1.COM",SUBRT1
370 SUBRT2=VARPTR(ARAY2(1)):BLOAD "SUBRT2.COM",SUBRT2
380 DEF SEG=SEGID: BLOAD"BSUB1.COM",0: DEF SEG
390 REM
400 GOTO 1010
410 ARAY1(3)=P3:ARAY1(4)=P4:ARAY1(5)=QY:ARAY1(6)=D3:ARAY1(7)=C3
420 SUBRT1=VARPTR(ARAY1(1)):CALL SUBRT1
430 RETURN
440 ARAY2(3)=Q3:ARAY2(4)=Q4:ARAY2(5)=PX:ARAY2(6)=D4:ARAY2(7)=C4
450 SUBRT2=VARPTR(ARAY2(1)):CALL SUBRT2
460 RETURN
470 P3=P1:P4=P2:QY=Q1-1:C3=C1:D3=196:GOSUB 410
480 QY=Q2:D3=196:GOSUB 410
490 P3=P1:P4=P1:QY=Q1-1:C3=C1:D3=218:GOSUB 410
500 P3=P2:P4=P2:QY=Q1-1:C3=C1:D3=191:GOSUB 410
510 PX=P1:Q3=Q1:Q4=Q2-1:C4=C1:D4=179:GOSUB 440
520 PX=P2:GOSUB 440
530 P3=P1:P4=P1:QY=Q2  :C3=C1:D3=192:GOSUB 410
540 P3=P2:P4=P2:QY=Q2  :C3=C1:D3=217:GOSUB 410
550 RETURN
560  P1=B1:Q1=B2:P2=B3:Q2=B4:C1=3
570  FOR I=1 TO F2
580  IF (INIT=0) THEN GOSUB 470
590 CUR=F1+F2-I:IF (CUR>0) THEN GOSUB 710
600 IF (INIT<>0) THEN CUR=F1+F2-I
610 IF (INIT<>0) THEN 630
620 IF ((I<>F2) AND (INIT=0)) THEN  P3=P1:P4=P2-1:QY=Q2:D3=&H20:C3=0:GOSUB 410
630 IF ((I<> 1) AND (INIT=0)) THEN  P3=P1+D11:P4=P1:QY=Q1-1:D3=193:C3=C1:GOSUB 410
640 P1=P1-D11:P2=P2-D11:Q1=Q1+D10:Q2=Q2+D10
650 NEXT I
660 RETURN
670 P1=L1:Q1=L2+2:P2=B3:Q2=24:C1=4:GOSUB 470:RETURN
680 GOSUB 1110
690 P3=CXX:P4=P3:QY=CYY:D3=219:C3=12:GOSUB 410
700 RETURN
710 REM DISPLAY THE CURRENT CONTENT OF THE FRAME
720 IF (FSW=1) AND (F3<>CUR) THEN GOTO 830
730 LOCATE B2+D10*(I-1),B1-(I-1)+1:IF (CUR=<36) THEN PRINT USING "  G:##  ";CUR;:GOTO 750 ELSE PRINT SPACE$(40);
740 LOCATE B2+D10*(I-1)+1,B1-(I-1)+1:PRINT SPACE$(42);: GOTO 840
750 LOCATE B2+D10*(I-1),B1-(I-1)+10:IF (DT(CUR,1)=-1) THEN ELSE GOTO 765
760 PRINT "--/--/--";:GOTO 780
765 IF (DT(CUR,1)>9) THEN 770
766 PRINT USING "##/##/0#";DT(CUR,2);DT(CUR,3);DT(CUR,1);:GOTO 780
770 PRINT USING "##/##/##";DT(CUR,2);DT(CUR,3);DT(CUR,1);
780 LOCATE B2+D10*(I-1),B1-(I-1)+20:TI=TM(CUR,1):GOSUB 900:PRINT ":";:TI=TM(CUR,2):GOSUB 900
790 LOCATE B2+D10*(I-1)+1,B1-(I-1)+10:PRINT CT$(AE(CUR,1));SPACE$(23);
800 LOCATE B2+D10*(I-1)+1,B1-(I-1)+20
810 ON AE(CUR,1)-1 GOSUB 850,860,870
820 LOCATE B2+D10*(I-1)+1,B1-(I-1)+40:IF (AB(CUR,1)<>0) THEN PRINT USING "AEB:#";AB(CUR,5); ELSE PRINT SPACE$(5);
830 LOCATE B2+D10*(I-1),B1-(I-1)+40:IF (ER(CUR)=1) THEN PRINT "X"; ELSE PRINT SPACE$(1);
840 RETURN
850 PRINT TV$(AE(CUR,2));SPACE$(2);:RETURN
860 PRINT AV$(AE(CUR,3));SPACE$(2);:RETURN
870 PRINT TV$(AE(CUR,2));SPACE$(2);AV$(AE(CUR,3));:RETURN
875 IF (TI<0) THEN GOTO 940  ELSE GOTO 920
880 REM print date
890 IF (TI<0) THEN GOTO 940  ELSE GOTO 950
900 REM print time
910 IF (TI<0) THEN GOTO 945
920 IF (TI>9) THEN GOTO 950
930 PRINT USING "0#";TI;:RETURN
940 PRINT JT$;:JT$="--":RETURN
945 PRINT "--";:RETURN
950 PRINT USING "##";TI;:RETURN
960 IMCU=1:SC=1:LOCATE V2+6,V1+16:PRINT W8
970 LOCATE V2-1,V1+7:PRINT W7;:PRINT USING "##";F3;
980 LOCATE V2,V1+7:PRINT W1:GOSUB 3670:GOSUB 3690:LOCATE V2+1,V1+7 :PRINT W2:GOSUB 3710:P3=V1+23:P4=P3:QY=V2:D3=219:C3=12:GOSUB 410:IF (AB(F3,1)=1) THEN GOSUB 1560
990 RETURN
1000  REM  main
1010 GOSUB 560: INIT=1: GOSUB 680
1020 ON KEY (1) GOSUB 1120 :REM SET
1030 ON KEY (3) GOSUB 1210 :REM SETEND
1040 ON KEY (10) GOSUB 4330 :REM ---> END
1050 ON KEY (11) GOSUB 3900
1060 ON KEY (14) GOSUB 3980
1070 IF (INKEY$<>"") THEN 1070
1080 KEY (11) ON:KEY (14) ON:KEY (1) ON :KEY(3) ON:KEY (10) ON
1090 MDI=0: SDI=1: GOSUB 1390: KEY ON
1100 IF MDI<>0 THEN 1020 ELSE 1100
1110 FOR I=L2+1 TO 24  :P3=L1  :P4=B3  :QY=I:D3=&H20:C3=0:GOSUB 410:NEXT I:      RETURN
1120 REM  set        syori
1130 IF MDI=0 THEN MDI=1 ELSE RETURN
1140 GOSUB 4290:GOSUB 1110:GOSUB 670:GOSUB 960:GOSUB 1420:ST=1
1150 IF (INKEY$<>"") THEN 1150
1160 KEY (1) ON:KEY (4) ON:KEY (5) ON:KEY (11) ON:KEY (12) ON:KEY (13) ON:KEY (14) ON: SDI=0
1170 IF SDI<> 0 THEN 1150
1180 W$=INKEY$ :IF (W$="") THEN 1170 ELSE SDI=1: GOSUB 4290
1190 IF ASC(W$)=13 THEN GOSUB 3820:ST=0:FSW=1:GOSUB 560:FSW=0:GOSUB 680:RETURN
1200 GOTO 1170
1210 REM  SET END
1220 IF MDI=0 THEN MDI=1 ELSE RETURN
1230 GOSUB 4290 : REM   key  off
1240 P3=CXX: P4=CXX: QY=CYY: D3=&H20: C3=0: GOSUB 410
1250 IF F1<>1 OR F3<>1 THEN ELSE 1270
1260 CXX=L1+1: CYY=B2+(F2-1)*D10: F1=1: F3=1: GOSUB 560
1270 REND=0: GOSUB 1110  : GOSUB 670
1280 LOCATE V2,V1+20:PRINT "DATE :";:LOCATE V2+2,V1+20:PRINT "TIME :";:LOCATE V2+5,V1+15:PRINT "SET AGAIN ---> [ ENTER ]";
1290 KEY 1,"":KEY 3,"":KEY 5,F5$+CHR$(13):KEY 10,""
1300 ON KEY (5) GOSUB 4380 :REM ---> * run syori *
1310 IF (INKEY$ <> "") THEN 1310
1320 IF REND<>0 THEN 1350
1330 KEY (5) ON : W$=INKEY$ :IF W$="" THEN 1320
1340 KEY(5) STOP: IF (ASC(W$)<>13) THEN 1320
1350 F1=1: GOSUB 560
1360 P3=CXX: P4=CXX: QY=CYY: D3=219: C3=12: GOSUB 410
1370 GOSUB 4290:GOSUB 1110: GOSUB 1390:RETURN
1380 REM set f-key   f(1) & f(3)
1390 KEY 1,F1$: KEY 2,"": KEY 3,F3$: KEY 4,""
1400 KEY 5,"" : KEY 6,"": KEY 7,"": KEY 8,"":KEY 9,"": KEY 10,F10$
1410 RETURN
1420 KEY 1,FB2$:KEY 3,"": KEY 4,FB4$
1430 KEY 5,FB5$: KEY 10,""
1440 ON KEY (1) GOSUB 1520 :REM AEB
1450 ON KEY (4) GOSUB 3030 :REM CLR
1460 ON KEY (5) GOSUB 2670 :REM RESET
1470 ON KEY (11) GOSUB 1670 :REM UP
1480 ON KEY (14) GOSUB 2220 :REM DOWN
1490 ON KEY (13) GOSUB 2820 :REM ->
1500 ON KEY (12) GOSUB 2940 :REM <-
1510 RETURN
1520 REM aeb on/off
1530 IF SDI=0 THEN SDI=1 ELSE RETURN
1540 GOSUB 4290:AB(F3,1)=AB(F3,1) XOR 1: IF (AB(F3,1)<>0) THEN 1560 ELSE 1570
1550 REM prin aeb
1560 LOCATE V2+3,V1+7:PRINT FB2$:LOCATE V2+3,V1+23:PRINT W3:GOSUB 1590:LOCATE V2+3,V1+40:PRINT W4:GOSUB 1610:LOCATE V2+4,V1+23:PRINT W5:GOSUB 1630:LOCATE V2+4,V1+40:PRINT W6 :GOSUB 1650:RETURN
1570 LOCATE V2+3,V1+7:PRINT SPACE$(45);:LOCATE V2+4,V1+17:PRINT SPACE$(40);:IF SC>8 THEN SC=1:GOSUB 2960
1580 RETURN
1590 REM print step
1600 LOCATE V2+3,V1+28:PRINT USING "+#.##";FNZEV(AB(F3,4));:RETURN
1610 REM print from
1620 LOCATE V2+3,V1+46:PRINT USING "+#.##";FNZEV(AB(F3,2));:RETURN
1630 REM print to
1640 AB(F3,3)=AB(F3,2)+AB(F3,4)*(AB(F3,5)-1):LOCATE V2+4,V1+45:PRINT USING "+##.##"; FNZEV(AB(F3,3));:RETURN
1650 REM print fr
1660 LOCATE V2+4,V1+28:PRINT AB(F3,5);:RETURN
1670 IF SDI=0 THEN SDI=1 ELSE RETURN
1680 GOSUB 4290: ON SC GOTO 1740,1810,1700,1860,1900,1990,2020,2070,2090,2130,2180
1690 REM year up
1700 IF (DT(F3,1)=-1) THEN DT(F3,1)=87:GOTO 1720
1710 IF (DT(F3,1)=99) THEN DT(F3,1)=0 ELSE DT(F3,1)=DT(F3,1)+1
1720 TI=DT(F3,1):GOSUB 1970:GOSUB 875:GOTO 1780
1730 REM month up
1740 IF (DT(F3,2)=-1) THEN DT(F3,2)=1:GOTO 1760
1750 IF (DT(F3,2)=12) THEN DT(F3,2)=1:ELSE DT(F3,2)=DT(F3,2)+1
1760 TI=DT(F3,2)
1770 GOSUB 1970:GOSUB 880
1780 ID=1:GOSUB 1940:IF (DT(F3,3)<>CD) THEN ELSE RETURN
1790 DT(F3,3)=CD:TC=SC:SC=2:TI=CD
1800 GOSUB 1970:GOSUB 880:SC=TC:RETURN
1810 REM day up
1820 IF DT(F3,3)=-1 THEN DT(F3,3)=1:GOTO 1840
1830 DT(F3,3)=DT(F3,3)+1:ID=1
1840 GOSUB 1940:DT(F3,3)=CD
1850 TI=CD:GOSUB 1970:GOSUB 880:RETURN
1860 REM hour up
1870 IF TM(F3,1)=23 THEN TM(F3,1)=0 ELSE TM(F3,1)=TM(F3,1)+1
1880 TI=TM(F3,1):GOTO 1920
1890 REM minute up
1900 IF TM(F3,2)=59 THEN TM(F3,2)=0 ELSE TM(F3,2)=TM(F3,2)+1
1910 TI=TM(F3,2)
1920 GOSUB 1970:GOSUB 900:RETURN
1930 REM * set date check
1940 CY=DT(F3,1):CM=DT(F3,2):CD=DT(F3,3)
1950 GOSUB 4050:RETURN
1960 REM yy/mm/dd  hh:mm clear
1970 LOCATE V2+SP(SC,1),V1+SP(SC,2)+1:RETURN
1980 REM ae up
1990 IF AE(F3,1)=4 THEN AE(F3,1)=1 ELSE AE(F3,1)=AE(F3,1)+1
2000 GOSUB 3710:RETURN
2010 REM tv,av up
2020 IF AE(F3,1)=3 THEN 2060
2030 IF (AE(F3,2)=35) THEN RETURN ELSE IF (AE(F3,1)<>4) THEN AE(F3,2)=AE(F3,2)+1:GOSUB 3740:RETURN
2040 AE(F3,2)=AE(F3,2)+1:GOSUB 3780:RETURN
2050 REM
2060 IF AE(F3,3)=21 THEN RETURN ELSE AE(F3,3)=AE(F3,3)+1:GOSUB 3760:RETURN
2070 IF AE(F3,3)=21 THEN RETURN ELSE AE(F3,3)=AE(F3,3)+1:GOSUB 3780:GOSUB 4260:RETURN
2080 REM step up
2090 IF (AB(F3,4)=8) THEN RETURN
2100 AB(F3,4)=AB(F3,4)+1
2110 IF (AB(F3,4)=0) THEN AB(F3,4)=1
2120 GOSUB 1600:GOSUB 1640:RETURN
2130 REM from up
2140 IF (AB(F3,2)=20) THEN RETURN
2150 AB(F3,2)=AB(F3,2)+1
2160 GOSUB 1620:GOSUB 1640:RETURN
2170 REM fr up
2180 IF (AB(F3,5)=9) THEN RETURN
2190 AB(F3,5)=AB(F3,5)+1
2200 GOSUB 1660:GOSUB 1640:RETURN
2210 REM DOWN
2220 IF SDI=0 THEN SDI=1 ELSE RETURN
2230 GOSUB 4290: ON SC GOTO 2290,2320,2250,2360,2400,2440,2470,2520,2540,2580,2630
2240 REM year down
2250 IF (DT(F3,1)=-1) THEN DT(F3,1)=86:GOTO 2270
2260 IF (DT(F3,1)=0 ) THEN DT(F3,1)=99 ELSE DT(F3,1)=DT(F3,1)-1
2270 GOTO 1720
2280 REM month down
2290 IF (DT(F3,2)=-1) THEN DT(F3,2)=12 :GOTO 2310
2300 IF (DT(F3,2)=1 ) THEN DT(F3,2)=12:ELSE DT(F3,2)=DT(F3,2)-1
2310 GOTO 1760
2320 REM day down
2330 IF DT(F3,3)=-1 THEN DT(F3,3)=31:ID=0:GOTO 1840
2340 IF DT(F3,3)=1 THEN DT(F3,3)=31: ELSE DT(F3,3)=DT(F3,3)-1
2350 ID=0:GOTO 1840
2360 REM hour down
2370 IF TM(F3,1)=<0 THEN TM(F3,1)=23 ELSE TM(F3,1)=TM(F3,1)-1
2380 TI=TM(F3,1):GOTO 2420
2390 REM minute down
2400 IF TM(F3,2)=<0 THEN TM(F3,2)=59 ELSE TM(F3,2)=TM(F3,2)-1
2410 TI=TM(F3,2)
2420 GOSUB 1970:GOSUB 900:RETURN
2430 REM ae down
2440 IF AE(F3,1)=1 THEN AE(F3,1)=4 ELSE AE(F3,1)=AE(F3,1)-1
2450 GOSUB 3710:RETURN
2460 REM tv,av down
2470 IF AE(F3,1)=3 THEN GOTO 2510
2480 IF (AE(F3,2)=1) THEN RETURN ELSE IF (AE(F3,1)<>4) THEN AE(F3,2)=AE(F3,2)-1:GOSUB 3740:RETURN
2490 AE(F3,2)=AE(F3,2)-1:GOSUB 3780:RETURN
2500 REM
2510 IF AE(F3,3)=1 THEN RETURN ELSE AE(F3,3)=AE(F3,3)-1:GOSUB 3760:RETURN
2520 IF AE(F3,3)=1 THEN RETURN ELSE AE(F3,3)=AE(F3,3)-1:GOSUB 3780:GOSUB 4260:RETURN
2530 REM step down
2540 IF (AB(F3,4)=-8) THEN RETURN
2550 AB(F3,4)=AB(F3,4)-1
2560 IF (AB(F3,4)=0) THEN AB(F3,4)=-1
2570 GOSUB 1600:GOSUB 1640:RETURN
2580 REM from down
2590 IF (AB(F3,2)=-20) THEN RETURN
2600 AB(F3,2)=AB(F3,2)-1
2610 GOSUB 1620:GOSUB 1640:RETURN
2620 REM fr down
2630 IF (AB(F3,5)=2) THEN RETURN
2640 AB(F3,5)=AB(F3,5)-1
2650 GOSUB 1660:GOSUB 1640:RETURN
2660 REM RESET
2670 IF SDI=0 THEN SDI=1 ELSE RETURN
2680 GOSUB 4290:IF (SC=>9) THEN GOTO 2790:REM aeb ->
2690 IF (SC=6) THEN AE(F3,1)=1:GOSUB 3700:RETURN
2700 IF (SC=>7) THEN RETURN
2710 IF (SC=>4) THEN GOTO 2750
2720 FOR J=1 TO 3:DT(F3,J)=-1:NEXT:GOSUB 4230
2730 FOR SC=1 TO 3:TI=DT(F3,SC):GOSUB 1970:GOSUB 6200:GOSUB 880:NEXT
2740 SC=1:GOSUB 4260:RETURN
2750 FOR J=1 TO 2 :TM(F3,J)=-1:NEXT:GOSUB 4230
2760 FOR SC=4 TO 5 :TI=TM(F3,SC-3):GOSUB 1970:GOSUB 900:NEXT
2770 SC=4:GOSUB 4260:RETURN
2780 REM -> aeb
2790 AB(F3,2)=-2:AB(F3,3)=0:AB(F3,4)=2:AB(F3,5)=3
2800 GOSUB 1600:GOSUB 1620:GOSUB 1660:GOSUB 1640:RETURN
2810 REM ->
2820 IF SDI=0 THEN SDI=1 ELSE RETURN
2830 GOSUB 4290:P3=V1+SP(SC,2):P4=P3:QY=V2+SP(SC,1):D3=&H20:C3=0:GOSUB 410
2840 IF (SC=6) AND (AE(F3,1)<>1) THEN 2900
2850 IF (SC=6) AND (AB(F3,1)<>0) THEN SC=9 :GOTO  2910
2860 IF (SC=7) AND (AE(F3,1)=4)  THEN 2900
2870 IF (SC=7) AND (AB(F3,1)<>0) THEN SC=9 :GOTO 2910
2880 IF (SC=8) AND (AB(F3,1)<>0) THEN SC=9 :GOTO 2910
2890 IF (SC=6) OR (SC=7) OR (SC=8) OR (SC=11) THEN GOTO 2910
2900 SC = SC + 1
2910 P3=V1+SP(SC,2):P4=P3:QY=V2+SP(SC,1):D3=219:C3=12:GOSUB 410
2920 RETURN
2930 REM <-
2940 IF SDI=0 THEN SDI=1 ELSE RETURN
2950 GOSUB 4290:P3=V1+SP(SC,2):P4=P3:QY=V2+SP(SC,1):D3=&H20:C3=0:GOSUB 410
2960 IF (SC=1) THEN GOTO 3000
2970 IF (SC=9) AND (AE(F3,1)=1) THEN SC=6 :GOTO 3000
2980 IF (SC=9) AND (AE(F3,1)<>4)THEN SC=7 :GOTO 3000
2990 SC = SC - 1
3000 P3=V1+SP(SC,2):P4=P3:QY=V2+SP(SC,1):D3=219:C3=12:GOSUB 410
3010 RETURN
3020 REM Clear syori
3030 IF SDI=0 THEN SDI=1 ELSE RETURN
3040 GOSUB 4290:ON SC GOTO 3080,3060,3100,3120,3140,3210,3180,3240,3250,3270,3290
3050 REM  day   clr
3060 DT(F3,3)=1:TI=DT(F3,3):GOSUB 3170:GOTO 3110
3070 REM  month clr
3080 DT(F3,2)=1 :TI=DT(F3,2):GOSUB 3170:GOTO 3110
3090 REM  year  clr
3100 DT(F3,1)=87:TI=DT(F3,1):GOSUB 3170
3110 GOSUB 880 : RETURN
3120 REM  hour  clr
3130 TM(F3,1)=0:TI=TM(F3,1):GOSUB 3170:GOTO 3160
3140 REM  minute  clr
3150 TM(F3,2)=0:TI=TM(F3,2):GOSUB 3170
3160 GOSUB 900 :RETURN
3170 LOCATE V2+SP(SC,1),V1+SP(SC,2)+1:RETURN
3180 REM  tv & av  clr
3190 IF (AE(F3,1)=3) THEN 3220
3200 AE(F3,2)=25:IF (AE(F3,1)<>4) THEN GOSUB 3740 ELSE GOSUB 3780
3210 RETURN
3220 REM
3230 AE(F3,3)=11:GOSUB 3760: RETURN
3240 AE(F3,3)=11:GOSUB 3780: GOSUB 4260: RETURN
3250 REM  step  clr
3260 AB(F3,4)=2 :GOSUB 1600:GOSUB 1640: RETURN
3270 REM  from  clr
3280 AB(F3,2)=-2:GOSUB 1620:GOSUB 1640: RETURN
3290 REM  fr    clr
3300 AB(F3,5)=3 :GOSUB 1660:GOSUB 1640: RETURN
3310 REM  make vd
3320 FOR I=1 TO 36 :ER(I)=0 :NEXT
3330 FOR I=1 TO 36
3340  IF (TM(I,1)=-1) THEN  ER(I)=1:GOTO 3530
3350  VT(I)=TM(I,1)*60+TM(I,2)
3360  J=I
3375  J=J-1 :IF J=0 THEN J=I :GOTO 3400
3380  IF ER(J)<>0 THEN GOTO 3375
3400  IF DT(I,1)=-1 THEN ELSE 3480
3410  IF (VD(J,2)=0) OR (J=I) THEN VD(I,1)=-1 :VD(I,2)=0 :GOTO 3530
3420  IF VT(J) < VT(I) THEN ELSE GOTO 3440
3430  VD(I,1)=VD(J,1):VD(I,2)=VD(J,2):GOTO 3530
3440  CY=VD(J,1):CM=INT(VD(J,2)/100):CD=(VD(J,2) MOD 100)+1
3450  ID=1:GOSUB 4050
3460  VD(I,1)=CY:VD(I,2)=CM*100+CD:GOTO 3530
3470  REM
3480  VD(I,1)=DT(I,1):VD(I,2)=DT(I,2)*100+DT(I,3):IF (J=I) THEN 3530
3490  IF VD(I,1)>VD(J,1) OR (VD(J,1)=99 AND VD(I,1)=0) THEN 3530
3495  IF VD(I,1)<VD(J,1) THEN ER(I)=1: GOTO 3530
3500  IF VD(I,2)<VD(J,2) THEN ER(I)=1: GOTO 3530
3510  IF VD(I,2)>VD(J,2) THEN 3530
3520  IF VT(I)=<VT(J) THEN ER(I)=1
3530 NEXT I
3540 REM
3550 FOR I=1 TO 36
3560  VT(I)=VT(I)-1:IF VT(I)<0 THEN VT(I)=23*60+59 ELSE 3650
3570  IF VD(I,2)=0 THEN 3650
3580  CY=VD(I,1):CM=INT(VD(I,2)/100):CD=(VD(I,2) MOD 100)
3590  CD=CD-1:IF CD<1 THEN CD=31 ELSE 3640
3600  CM=CM-1:IF CM<1 THEN CM=12 ELSE 3620
3610  CY=CY-1:IF CY<0 THEN CY=99
3620  ID=0:GOSUB 4050
3630  VD(I,1)=CY
3640  VD(I,2)=CM*100+CD
3650 NEXT I
3660 RETURN
3670 REM  print date & time
3680 LOCATE V2,V1+24 :TI=DT(F3,2):JT$="mm":GOSUB 880:PRINT "/ ";:TI=DT(F3,3):JT$="dd":GOSUB 880:PRINT "/ ";:TI=DT(F3,1):JT$="yy":GOSUB 875 :RETURN
3690 LOCATE V2,V1+44:TI=TM(F3,1):GOSUB 900:PRINT ": ";:TI=TM(F3,2):GOSUB 900:RETURN
3700 REM print ae mode
3710 LOCATE V2+1,V1+24:PRINT CT$(AE(F3,1));SPACE$(30);
3720 ON AE(F3,1)-1 GOTO 3740,3760,3780
3730 RETURN
3740 REM print set TV
3750 LOCATE V2+1,V1+39:PRINT TV$(AE(F3,2));SPACE$(11);:RETURN
3760 REM print set AV
3770 LOCATE V2+1,V1+39:PRINT AV$(AE(F3,3));SPACE$(11);:RETURN
3780 REM print set AV & TV
3790 LOCATE V2+1,V1+34:PRINT "TV";:LOCATE V2+1,V1+39:PRINT TV$(AE(F3,2));SPACE$(6);
3800 LOCATE V2+1,V1+44:PRINT "AV";:LOCATE V2+1,V1+49:PRINT AV$(AE(F3,3));SPACE$(6);:RETURN
3810 REM
3820 REM return - key   syori
3830 IF DT(F3,1)=-1 OR DT(F3,2)=-1 OR DT(F3,3)=-1 THEN ELSE 3850
3840 FOR I=1 TO 3 :DT(F3,I)=-1:NEXT
3850 IF TM(F3,1)=-1 OR TM(F3,2)=-1 THEN ELSE 3870
3860 FOR I=1 TO 2 :TM(F3,I)=-1:NEXT
3870 GOSUB 3310:GOSUB 1110
3880 RETURN
3890 REM cursor   up
3900 IF MDI=0 THEN MDI=1 ELSE RETURN
3910 GOSUB 4290:IF (ST<>0) THEN RETURN ELSE P3=CXX:P4=CXX:QY=CYY:D3=&H20:C3=0:GOSUB 410:CXX=CXX+D11:CYY=CYY-D10
3920 F3=F3+1: IF(F3>36) THEN F3=36
3930 IF (CXX>(B1+1)) THEN CXX=B1+1: CYY=B2  :F1=F1+1 : ELSE 3960
3940 IF (F1>(36-F2+1))  THEN  F1=36-F2+1:BEEP:GOTO 3960
3950 GOSUB 560 :P3=CXX:P4=CXX:QY=CYY:D3=219:C3=12:GOSUB 410 :RETURN
3960            P3=CXX:P4=CXX:QY=CYY:D3=219:C3=12:GOSUB 410 :RETURN
3970 REM cursor   down
3980 IF MDI=0 THEN MDI=1 ELSE RETURN
3990 GOSUB 4290: IF(ST<>0) THEN RETURN ELSE P3=CXX:P4=CXX:QY=CYY:D3=&H20:C3=0:GOSUB 410:CXX=CXX-D11:CYY=CYY+D10
4000 F3=F3-1:IF(F3<1 ) THEN F3=1
4010 IF (CXX<(B1-(F2-1)+1)) THEN CXX=B1-(F2-1)+1: CYY=B2+(F2-1)*D10 :F1=F1-1 : ELSE 4040
4020 IF (F1<1) THEN  F1=1:BEEP :GOTO 4040
4030 GOSUB 560 :P3=CXX:P4=CXX:QY=CYY:D3=219:C3=12:GOSUB 410 :RETURN
4040            P3=CXX:P4=CXX:QY=CYY:D3=219:C3=12:GOSUB 410 :RETURN
4050 REM date check
4060 IF (CM<>2) AND (CD>30) THEN 4130
4070 IF (CM<>2) OR  (CD<29) THEN RETURN
4080 REM 2 gatu
4090 IF ((CY MOD 4)=0) OR CY=-1 THEN 4110
4100 IF (CD>28) AND (ID=1) THEN  4120 ELSE CD=28:RETURN
4110 IF (CD>29) AND (ID=1) THEN  ELSE CD=29 :RETURN
4120 CD=1:CM=3:RETURN
4130 IF (CD>31) THEN 4180
4140 IF (CM>=8) THEN 4220
4150 REM 1-7 gatu
4160 IF ((CM MOD 2)<>0) THEN RETURN
4170 IF (ID=1) THEN ELSE CD=30:RETURN
4180 CD=1 : CM=CM+1
4190 IF (CM<13) THEN RETURN ELSE CM=1:CY=CY+1
4200 IF (CY<100) THEN RETURN ELSE CY=0 RETURN
4210 REM 8-12 gatu
4220 IF ((CM MOD 2)=0) THEN RETURN ELSE 4170
4230 REM off cursor
4240 P3=V1+SP(SC,2):P4=P3:QY=V2+SP(SC,1):D3=&H20:C3=0:GOSUB 410
4250 RETURN
4260 REM on cursor
4270 P3=V1+SP(SC,2):P4=P3:QY=V2+SP(SC,1):D3=219:C3=12:GOSUB 410
4280 RETURN
4290 REM key off
4300 FOR I=14 TO 1 STEP-1 :KEY (I) STOP : NEXT  I
4310 RETURN
4320 REM   end  key syori
4330 IF MDI=0 THEN ELSE RETURN
4340 GOSUB 4290: CLS
4350 FOR I=1 TO 10:KEY  I ,W9(I):NEXT
4360 ON ERROR GOTO 0:END
4370 REM  ******** run  syori *********
4380 GOSUB 4300: KEY 5,"STOP"
4390 LOCATE V2+5,V1+15:PRINT SPACE$(25)
4400 ON TIMER(1) GOSUB 5020: ON KEY(5) GOSUB 5000
4401 SMSG$=CHR$(&H5A)+CHR$(&HA7)+CHR$(2)+CHR$(&H80)+CHR$(0)+CHR$(&H8)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0):       REM   AEB off
4402 GOSUB 5050: IF RVAL<>0 THEN 5000
4403 SMSG$=CHR$(&H5A)+CHR$(&HA3)+CHR$(&H2)+CHR$(&H0)+CHR$(&H4)+CHR$(&H0)+CHR$(CBUF MOD &H100)+CHR$(CBUF\&H100)+CHR$(2)+CHR$(0)+CHR$(1)
4404 GOSUB 5050: IF RVAL<>0 THEN 5000
4405 DEF SEG=SEGID: PROG=(PEEK(CBUF)) AND 4 : DEF SEG
4410 KEY(5) ON: LS=0: RF=1: GOTO 4430
4420 LOCATE V2,V1+20: PRINT SPACE$(15): LOCATE V2+2,V1+20: PRINT "---  WAITING  ---"
4430 RL=0: SON=1 :CDATE=0
4440 IF ER(RF)<>0 THEN 4980
4450 '  FRAME UP
4460 IF F1<>RF THEN F1=RF: GOSUB 560
4470 CN=CC(AE(RF,1)): TV=(AE(RF,2)-1)*4+&H10: AV=(AE(RF,3)-1)*4+&H8
4480 IF AE(RF,1)=1 THEN CA=0 ELSE CA=&H20*(2^(AE(RF,1)-2))
4482 SMSG$=CHR$(&H5A)+CHR$(&HA5)+CHR$(1)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0) :'sw1 on
4483 GOSUB 5050: IF RVAL<>0 THEN 5000
4484 IF AE(RF,1)=0 AND PROG=4 THEN PP=4 ELSE PP=0
4485 SMSG$=CHR$(&H5A)+CHR$(&HA7)+CHR$(2)+CHR$(&H80)+CHR$(PP)+CHR$(4)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0) :'prog on off
4486 GOSUB 5050: IF RVAL<>0 THEN 5000
4490 IF AB(RF,1)=0 THEN 4610
4500 FOR R2=0 TO 3
4510    SMSG$=CHR$(&H5A)+CHR$(&HA7)+CHR$(&H30+R2)+CHR$(&H80)+CHR$(AB(RF,R2+2) AND &HFF)+CHR$(&HFF)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)
4520    GOSUB 5050: IF RVAL<>0 THEN 5000
4530    FOR I=0 TO 500: NEXT
4540 NEXT R2
4550 FOR I=0 TO 200: NEXT
4600 '  READ DATE & TIME
4610 SMSG$=CHR$(&H5A)+CHR$(&HA3)+CHR$(&H8)+CHR$(&H0)+CHR$(&HD)+CHR$(&H0)+CHR$(CBUF MOD &H100)+CHR$(CBUF\&H100)+CHR$(6)+CHR$(0)+CHR$(1)
4620 GOSUB 5050: IF RVAL<>0 THEN 5000
4630 DEF SEG=SEGID: RYEAR=FNBCD(CBUF): RDAY=FNBCD(CBUF+1)*100+FNBCD(CBUF+2)
4640 HOUR=FNBCD(CBUF+3):MIN=FNBCD(CBUF+4): SEC=FNBCD(CBUF+5):DEF SEG
4642 RTIME=HOUR*60+MIN
4645 IF RYEAR=99 AND VD(RF,1)=0 THEN 4680
4647 IF VD(RF,1)>RYEAR AND VD(RF,1)<>-1 THEN 4680
4650 IF VD(RF,1)<RYEAR AND VD(RF,1)<>-1 THEN 4980
4655 IF VD(RF,1)=RYEAR AND VD(RF,2)<RDAY AND VD(RF,1)<>-1 THEN 4980
4660 IF(VD(RF,1)=RYEAR AND VD(RF,2)=RDAY) OR (VD(RF,1)=-1 AND VT(RF)>= RTIME) THEN RL=1
4670 IF RL=1 AND VT(RF)<RTIME THEN 4980
4680 LOCATE V2,V1+20: PRINT "DATE :": LOCATE V2+2,V1+20: PRINT "TIME :";SPACE$(13)
4690 IF RL=1 AND VT(RF)=RTIME THEN LS=60-SEC: TIMER ON: GOTO 4760 ELSE HOUR$=STR$(HOUR):MIN$=STR$(MIN):SEC$=STR$(SEC):TIME$=RIGHT$(HOUR$,LEN(HOUR$)-1)+":"+RIGHT$(MIN$,LEN(MIN$)-1)+":"+RIGHT$(SEC$,LEN(SEC$)-1)
4700 '
4710 '
4720 LOCATE V2,V1+27: IF RYEAR>9 THEN 4725
4723 PRINT USING "##/##/0#";RDAY\100,RDAY MOD 100,RYEAR : GOTO 4730
4725 PRINT USING "##/##/##";RDAY\100,RDAY MOD 100,RYEAR
4730 LOCATE V2+2,V1+27: PRINT TIME$
4740 RTIME=VAL(MID$(TIME$,1,2))*60+VAL(MID$(TIME$,4,2))
4745 IF REND<>0 THEN 5000
4746 IF RL=1 AND RTIME=VT(RF) THEN  LS=60 :TIMER ON :GOTO 4760
4747 IF RTIME=0 AND CDATE=1 THEN 4420
4748 IF RTIME>=1439 THEN CDATE=1 ELSE CDATE=0
4750 GOTO 4730
4760 SMSG$=CHR$(&H5A)+CHR$(&HA5)+CHR$(1)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)
4770 GOSUB 5050: IF RVAL<>0 THEN 5000
4780 IF LS <= 5 AND SON = 1 THEN SON=0 ELSE 4830
4790 IF AB(RF,1)=0 THEN 4810
4791 SMSG$=CHR$(&H5A)+CHR$(&HA7)+CHR$(2)+CHR$(&H80)+CHR$(&HFF)+CHR$(&H8)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)
4792 GOSUB 5050: IF RVAL<>0 THEN 5000
4793 SMSG$=CHR$(&H5A)+CHR$(&HA7)+CHR$(2)+CHR$(&H80)+CHR$(&HFF)+CHR$(&H8)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)
4800 GOSUB 5050: IF RVAL<>0 THEN 5000
4810 SMSG$=CHR$(&H5A)+CHR$(&HA5)+CHR$(1)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0) :'sw1 on
4812 GOSUB 5050: IF RVAL<>0 THEN 5000
4815 SMSG$=CHR$(&H5A)+CHR$(&HAE)+CHR$(CN)+CHR$(CA)+CHR$(TV)+CHR$(AV)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)
4820 GOSUB 5050: IF RVAL<>0 THEN 5000
4830 IF REND<>0 THEN 5000
4840 IF LS=0 THEN TIMER OFF ELSE 4780
4850 '  RELESE
4860 IF AB(RF,1)=0 THEN ELSE 4890
4870 GOSUB 6000: IF RVAL<>0 THEN 5000 ELSE 4980
4880 '  AEB ON
4890 FOR R1=1 TO AB(RF,5)
4900    GOSUB 6000: IF RVAL<>0 THEN 5000
4910    IF REND<>0 THEN 5000
4930    FOR I=0 TO 500: NEXT I
4932    SMSG$=CHR$(&H5A)+CHR$(&HA5)+CHR$(1)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0) :'sw1 on
4934    GOSUB 5050: IF RVAL<>0 THEN 5000
4936    SMSG$=CHR$(&H5A)+CHR$(&HAE)+CHR$(CN)+CHR$(CA)+CHR$(TV)+CHR$(AV)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)
4938    GOSUB 5050: IF RVAL<>0 THEN 5000
4940  NEXT R1
4950 SMSG$=CHR$(&H5A)+CHR$(&HA7)+CHR$(2)+CHR$(&H80)+CHR$(0)+CHR$(&H8)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0):       REM   AEB off
4960 GOSUB 5050: IF RVAL<>0 THEN 5000
4970 '  NEXT ?
4980 IF REND<>0 THEN 5000
4990 RF=RF+1: IF RF>36 THEN ELSE 4420
5000 KEY(5) OFF: TIMER OFF
5002 IF PROG<>4 THEN 5005
5003 SMSG$=CHR$(&H5A)+CHR$(&HA7)+CHR$(2)+CHR$(&H80)+CHR$(4)+CHR$(4)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)
5004 GOSUB 5050
5005 REND=1: W$=CHR$(&H13): RETURN
5010 '  ON TIME
5020 IF LS=0 THEN SEC=SEC+1: RETURN
5030 LS=LS-1: LOCATE V2+2,V1+27: PRINT USING " ## sec ";LS: RETURN
5040 '  DATA COM
5050 DEF SEG=SEGID: R$=USR0(SMSG$): RVAL=PEEK(0): DEF SEG: RETURN
5060 '  SEND RELESE COMMAND
5070 SMSG$=CHR$(&H5A)+CHR$(&HAE)+CHR$(&H40+CN)+CHR$(CA)+CHR$(TV)+CHR$(AV)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)
5080 GOSUB 5050: IF RVAL<>0 THEN RETURN
5100 RETURN
6000 '  SEND RELRESE FOR AEB
6010 SMSG$=CHR$(&H5A)+CHR$(&HAE)+CHR$(&H40+CN)+CHR$(CA)+CHR$(TV)+CHR$(AV)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)+CHR$(0)
6020 GOSUB 5050: IF RVAL<>0 THEN RETURN
6025    FOR I=0 TO 150: NEXT I
6030 SMSG$=CHR$(&H5A)+CHR$(&HA3)+CHR$(&HFE)+CHR$(&H1F)+CHR$(&HFF)+CHR$(&H1F)+CHR$(CBUF MOD &H100)+CHR$(CBUF\&H100)+CHR$(2)+CHR$(0)+CHR$(1)
6040 GOSUB 5050: IF RVAL<>0 THEN RETURN
6050 DEF SEG=SEGID: CTVD=PEEK(CBUF): DEF SEG
6060 IF CTVD>&H40 THEN RETURN
6070 TT=2^((&H38-CTVD)/8)+.5
6080 IF REND<>0 THEN 5000
6090 KEY(5) OFF:ON TIMER(TT) GOSUB 6110:TIMER ON
6100 IF TT>=1 THEN 6100 ELSE TIMER OFF:KEY(5) ON:ON TIMER(1) GOSUB 5020:RETURN
6110 TIMER OFF:TT=0:RETURN
6190 '
6200 IF SC=1 THEN JT$="mm"
6210 IF SC=2 THEN JT$="dd"
6220 IF SC=3 THEN JT$="yy"
6230 RETURN
